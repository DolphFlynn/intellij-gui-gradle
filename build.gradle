plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
    maven { url = uri('https://www.jetbrains.com/intellij-repository/releases') }
    maven { url = uri('https://packages.jetbrains.team/maven/p/ij/intellij-dependencies/') }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    guiGenerationTask
}

dependencies {
    guiGenerationTask(
            'com.jetbrains.intellij.java:java-compiler-ant-tasks:243.23654.166'
    )
    api(
            'com.jetbrains.intellij.java:java-gui-forms-rt:243.23654.166'
    )
}

tasks.withType(JavaCompile).configureEach {
    def mainSourceSets = project.sourceSets.main

    doLast {
        mainSourceSets.output.classesDirs.each { mkdir(it) }

        ant.taskdef(
                name: 'javac2',
                classname: 'com.intellij.ant.Javac2',
                classpath: configurations.guiGenerationTask.asPath
        )

        ant.javac2(
                srcdir: mainSourceSets.java.srcDirs.join(':'),
                classpath: mainSourceSets.compileClasspath.asPath,
                destdir: mainSourceSets.output.classesDirs.getSingleFile(),
                release: targetCompatibility,
                includeAntRuntime: false
        )
    }
}

jar {
    manifest {
        attributes("Main-Class": "Main")
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}